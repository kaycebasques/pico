import("//build_overrides/pigweed.gni")
import("$dir_pigweed/targets/host/target_toolchains.gni")

host = {
  _excluded_members = [
    "defaults",
    "name",
  ]
  _excluded_defaults = [
    "pw_trace_BACKEND",
    "pw_trace_tokenizer_time",
  ]
  host = {
    name = "host"
    _toolchain_base = pw_target_toolchain_host.clang_debug
    forward_variables_from(_toolchain_base, "*", _excluded_members)
    defaults = {
      forward_variables_from(_toolchain_base.defaults, "*", _excluded_defaults)
      pw_log_BACKEND = dir_pw_log_string
      pw_log_string_HANDLER_BACKEND = "//src:my_log_backend"
      # GN asks us to first re-assign to an empty list to explicitly confirm
      # that we want to overwrite this variable.
      pw_build_LINK_DEPS = []
      pw_build_LINK_DEPS = [
        "$dir_pw_assert:impl",
        "$dir_pw_log:impl",
        "$dir_pw_log_string:handler.impl",
      ]
    }
  }
}

toolchains_list = [ host.host ]
